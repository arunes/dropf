; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1F726311-656B-42E4-BD4D-D780833F35E9}
AppName=dropf
AppVerName=dropf 0.2.8
AppVersion=0.2.8
VersionInfoVersion=0.2.8
AppPublisher=arunes
AppPublisherURL=http://arunes.com/
AppSupportURL=http://dropf.com/
AppUpdatesURL=http://dropf.com/
DefaultDirName={pf}\dropf\
UninstallDisplayIcon={app}\dropf.exe
SetupIconFile="D:\Projects\Personal\dropf\dropf-app\dropf\dropf.ico"
; DisableDirPage=yes
DefaultGroupName=dropf
; LicenseFile=D:\Works\Web\KazanciHukuk\_Editable\program\license.rtf
OutputDir=D:\Projects\Personal\dropf\dropf-app\_source\setup
OutputBaseFilename=dropf-setup
Compression=lzma2
SolidCompression=yes
WizardImageFile=setup.bmp
WizardSmallImageFile=setup-small.bmp

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"; InfoBeforeFile:"version-history-en.rtf"; LicenseFile: "license-en.rtf"
Name: tr; MessagesFile: "compiler:Languages\Turkish.isl"; InfoBeforeFile:"version-history-tr.rtf"; LicenseFile: "license-tr.rtf"

[CustomMessages]
tr.dotnetmissing=Bu programýn çalýþmasý için bilgisayarýnýzda .NET Framework v4.0 kurulu olmalýdýr. Lütfen .NET Framework v4.0 programýný kurun ve dropf kurulumunu yeniden çalýþtýrýn.%n%n.NET Framework v4.0 programýný þimdi indirmek istiyor musunuz?
en.dotnetmissing=The Microsoft .NET Framework 4.0 is not installed. Please install before running setup.%n%nDo you want to download .NET Framework v4.0 now?
tr.deletesettings=Kayýtlý ftp siteleri ve kullanýcý ayarlarýnýzý da silmek istiyor musunuz?
en.deletesettings=Do you want to remove ftp sites and user settings?
tr.frameworkurl=http://www.microsoft.com/downloads/tr-tr/details.aspx?familyid=9cfb2d51-5ff4-4491-b0e5-b386f32c0992&displaylang=tr
en.frameworkurl=http://www.microsoft.com/download/en/details.aspx?id=17851

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version — Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//
// service — Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
key: string;
install, serviceCount: cardinal;
success: boolean;
begin
key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;
// .NET 3.0 uses value InstallSuccess in subkey Setup
if Pos('v3.0', version) = 1 then begin
success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
end else begin
success := RegQueryDWordValue(HKLM, key, 'Install', install);
end;
// .NET 4.0 uses value Servicing instead of SP
if Pos('v4', version) = 1 then begin
success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
end else begin
success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
end;
result := success and (install = 1) and (serviceCount >= service);
end;

function InitializeSetup(): Boolean;
var ErrorCode: Integer;
begin
if not IsDotNetDetected('v4\Client', 0) then begin
    if (MsgBox(ExpandConstant('{cm:dotnetmissing}'), mbConfirmation, MB_YESNO) = idYes) then
    begin
      ShellExec('open',ExpandConstant('{cm:frameworkurl}'),'','',SW_SHOWNORMAL,ewNoWait,ErrorCode);
    end;
result := false;
end else
result := true;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var mRes : integer;
begin
  case CurUninstallStep of
    usUninstall:
      begin
        mRes := MsgBox(ExpandConstant('{cm:deletesettings}'), mbConfirmation, MB_YESNO or MB_DEFBUTTON2)
        if mRes = idYes then
          begin
             DelTree(ExpandConstant('{userappdata}\dropf'), True, True, True);
             DelTree(ExpandConstant('{localappdata}\arunes'), True, True, True);
          end;
      end;
  end;
end;

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\dropf.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\dropf.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\Hardcodet.Wpf.TaskbarNotification.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\ICSharpCode.SharpZipLib.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\ShellLink.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\SingleInstanceApplication.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\startdropf.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\Themes\*.*"; DestDir: {app}\Themes; Flags: ignoreversion recursesubdirs
Source: "D:\Projects\Personal\dropf\dropf-app\dropf\bin\Release\Languages\*.*"; DestDir: {app}\Languages; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\dropf"; Filename: "{app}\dropf.exe"
Name: "{group}\{cm:UninstallProgram,dropf}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\dropf"; Filename: "{app}\dropf.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\dropf"; Filename: "{app}\dropf.exe"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\dropf.exe"; Description: "{cm:LaunchProgram,dropf}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{{sendto}}\dropf.lnk"



